
dnl -*- m4 -*-
dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Id: configure.in,v 1.12 2004/07/08 22:32:19 jimg Exp $)
AC_INIT(www_int.cc)
AC_CONFIG_HEADER(config_www_int.h)
AC_CONFIG_AUX_DIR(../../../etc)

AC_PREFIX_DEFAULT("\$(DODS_ROOT)")

dnl DODS_ROOT is the fully qualified pathname to the root of the DODS
dnl subtree. 

DODS_GET_DODS_ROOT
DODS_DIRECTORY_VERSION

dnl `depend' is the command to use to generate dependencies for Makefiles.

depend=${dods_relative}/etc/depend.sh
AC_SUBST(depend)

dnl flags for the compilers and linkers - set these before locating the
dnl actual tools since some of the AC_PROG macros set these `flag variables'
dnl to default values otherwise.

AC_CANONICAL_HOST
AC_SUBST(host)

# Until further notice OS X port doesn't get -gstabs because of an
# apparent bug in their gcc 3.3 clone (a beta).  If you see this after
# 1/1/2005, you can probably remove it. Same is true for the Alpha/OSF5. 
# jhrg 04/13/04. 
case "${host_cpu}:${host_vendor}:${host_os}" in
     powerpc:apple:netbsd* | powerpc:apple:darwin* )
       CFLAGS=${CFLAGS-"-O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-O2 $CXXFLAGS"}
       break;;
     alphaev56:dec:osf5.*)
       CFLAGS=${CFLAGS-"-O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-O2 $CXXFLAGS"}
       break;;
    *:*:*)
       CFLAGS=${CFLAGS-"-gstabs -O2 $CFLAGS"}
       CXXFLAGS=${CXXFLAGS-"-gstabs -O2 $CXXFLAGS"}
       break;;
esac    

dnl Checks for programs.

AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_CPP
AC_PROG_INSTALL

DODS_GCC
DODS_PROG_LEX
DODS_PROG_GTAR
DODS_PROG_PERL
DODS_PROG_BISON

CXX=${CXX-g++}
AC_MSG_RESULT(Setting CXX to $CXX)
AC_SUBST(CXX)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for specific libraries
AC_CHECK_LIB(pthread, pthread_testcancel, 
	[LIBS="$LIBS -lpthread"
	 AC_DEFINE(_REENTRANT, , [Define this if linking with pthreads])])

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror strtol)

DODS_CHECK_SIZES

DODS_COMPRESSION_LIB
DODS_RX_LIB
DODS_WWW_LIB

DODS_EFENCE
DODS_DBNEW
DODS_DEBUG_OPTION

if test $ac_cv_sizeof_long -eq 4 
then
	ARCHFLAG=-DARCH_32BIT
	AC_SUBST(ARCHFLAG)
elif test $ac_cv_sizeof_long -eq 8
then
	ARCHFLAG=-DARCH_64BIT
	AC_SUBST(ARCHFLAG)
else
	AC_MSG_ERROR(Could not determine architecture size - 32 or 64 bits)
fi

dnl autoheader macros; tack some text at the top and bottom of config_dap.h.in

AH_TOP([#ifndef _config_www_int_h
#define _config_www_int_h])

AH_BOTTOM([/* Shorthand for gcc's unused attribute feature */
#if defined(__GNUG__) || defined(__GNUC__)
#define not_used __attribute__ ((unused))
#else
#define not_used 
#endif /* __GNUG__ || __GNUC__ */

#endif /* _config_www_int_h */])

AC_OUTPUT(Makefile js2h.pl, [chmod 775 js2h.pl])
